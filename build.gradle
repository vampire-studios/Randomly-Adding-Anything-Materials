buildscript {
	dependencies {
		classpath 'de.guntram.mcmod:crowdin-translate:1.3+1.17'
	}
	repositories {
		maven {
			name = 'CrowdinTranslate source'
			url = "https://minecraft.guntram.de/maven/"
		}
	}
}

plugins {
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	alias(libs.plugins.quilt.loom)
	alias(libs.plugins.loom.quiltflower)
	alias(libs.plugins.mappings.loom)
}

apply plugin: 'de.guntram.mcmod.crowdin-translate'

crowdintranslate.crowdinProjectName = 'randomly-adding-anything-mater'
crowdintranslate.minecraftProjectName = 'raa_materials'
crowdintranslate.verbose = true

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
	mavenLocal()
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://maven.terraformersmc.com/" }
	maven { url = "https://maven.quiltmc.org/repository/" }
	maven { url = "https://maven.parchmentmc.org" }
	maven { url = "https://jitpack.io" }
	maven { url = "https://maven.siphalor.de" }
	maven { url = "https://cursemaven.com" }
	maven { url = "https://mvn.devos.one/releases/" }
	maven { url = "https://mvn.devos.one/snapshots/" }
	maven { url = "https://api.modrinth.com/maven" }
	maven { url = "https://aperlambda.github.io/maven" }
	maven { url = "https://maven.cafeteria.dev" }
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" }
	maven { url = "https://aperlambda.github.io/maven" }
	maven { url = "https://maven.tterrag.com/" }
	maven { url = "https://maven.blamejared.com" }
	maven { url = "https://maven.wispforest.io/" }
	maven { url = "https://ladysnake.jfrog.io/artifactory/mods" }
	maven { url = "https://minecraft.guntram.de/maven/" }
	maven { url = "https://maven.theepicblock.nl" }
}

loom {
	accessWidenerPath = file("src/main/resources/raa_materials.accesswidener")
}

quiltflower {
	addToRuntimeClasspath.set(true)
	fromUrl("https://maven.quiltmc.org/repository/snapshot/org/quiltmc/quiltflower/1.9.0-SNAPSHOT/quiltflower-1.9.0-20220429.122922-1.jar")
}

dependencies {
	minecraft libs.minecraft
	mappings loom.layered {
		it.addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2"))
		it.parchment("org.parchmentmc.data:parchment-1.18.2:2022.03.13@zip")
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
	}

	modImplementation libs.quilt.loader

	modImplementation (libs.fabric.api) {
		force = true
	}

	modImplementation (libs.raa.core) {
		exclude group: "net.fabricmc.fabric-api"
	}

	modImplementation (libs.create) {
		exclude group: "net.fabricmc.fabric-api"
	}

	modImplementation(include("com.electronwill.night-config:core:3.6.3"))
	modImplementation(include("com.electronwill.night-config:toml:3.6.3"))

//	modImplementation (libs.spectrum) {
//		exclude group: "net.fabricmc.fabric-api"
//	}

	modImplementation (libs.crowdin.translate) {
		exclude group: "net.fabricmc.fabric-api"
	}
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 16
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

build {
	dependsOn downloadTranslations
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
	repositories {
		maven {
			url file("D:\\Development\\Work\\VampireStudiosMaven\\")
		}
	}
}
